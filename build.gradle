// QueryDSL 버전정보 추가
buildscript {
	ext {
		queryDslVersion = "5.0.0"
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	//querydsl 추가
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = 'com.slicequeue'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	/* --- springframework --- */
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	/* --- lombok --- */
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	/* --- database --- */
	runtimeOnly 'com.h2database:h2'
	testRuntimeOnly 'com.h2database:h2'

	//querydsl 추가
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
	implementation "com.querydsl:querydsl-apt:${queryDslVersion}"
	implementation "com.querydsl:querydsl-core:${queryDslVersion}"

	/* --- 추가 --- */
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus:1.9.5'

	/* --- logback & sl4j 취약점 --- */
	implementation 'ch.qos.logback:logback-core:1.2.10'
	implementation 'ch.qos.logback:logback-classic:1.2.10'
	implementation 'org.slf4j:slf4j-api:1.7.32'
	implementation 'org.slf4j:jul-to-slf4j:1.7.32'
	implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.17.1'
	implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
}

tasks.named('test') {
	useJUnitPlatform()
}

//QueryDSL 플로그인 설정 START

def querydslDir = "$buildDir/generated/querydsl" //QueryDSL로 생성된 소스가 저장되는 위치

querydsl {
	jpa = true //JPA 사용여부 설정
	querydslSourcesDir = querydslDir
}

sourceSets {
	main.java.srcDir querydslDir// 빌드시 컴파일이 필요한 소스위치
}

//QueryDSL 컴파일시 참조할 설정정보
compileQuerydsl{
	options.annotationProcessorPath = configurations.querydsl
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	querydsl.extendsFrom compileClasspath //외부라이브러리 컴파일 경로 상속
}

//QueryDSL 플로그인 설정 END
